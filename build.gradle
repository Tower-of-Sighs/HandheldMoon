plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "${archives_base_name}-fabric-${minecraft_version}"
}

loom {
    accessWidenerPath = file("src/main/resources/handheldmoon.accesswidener")

    runs {
        client2 {
            client()
            programArgs "--username", "Flechazo"
        }
        client {
            programArgs "--username", "TowerofSighs"
        }
    }
}

repositories {
    maven {
        name = "Gegy"
        url = uri("https://maven.gegy.dev")
    }
    maven {
        // Add curse maven to repositories
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        // Patchouli
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        name = "Ladysnake Libs"
        url = 'https://maven.ladysnake.org/releases'
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modCompileOnly("dev.lambdaurora.lambdynamiclights:lambdynamiclights-api:${ldl_version}")
    modImplementation("dev.lambdaurora.lambdynamiclights:lambdynamiclights-runtime:${ldl_version}")

    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${project.fcap_version}"

    modImplementation "curse.maven:sodium-394468:6260639"
    modImplementation "curse.maven:irisshaders-455508:6258195"

    modCompileOnly ("mezz.jei:jei-1.20.1-fabric-api:${jei_verison}")
    modImplementation ("mezz.jei:jei-1.20.1-fabric:${jei_verison}")

    // maven 的 trinkets mixin 会报错
    modImplementation files("libs/trinkets-3.7.2.jar")
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"

    modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

    modImplementation "curse.maven:starlight-521783:4572096"

    modImplementation "curse.maven:tweakeroo-297344:4623488"
    modImplementation "curse.maven:malilib-303119:4780782"

    // fuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuuck loom
    modImplementation "curse.maven:tacz-refabricated-1334246:7281251"
    modImplementation files("libs/bcel-6.6.1.jar")
    modImplementation files("libs/commons-math3-3.6.1.jar")
    modImplementation files("libs/luaj-core-3.0.8-figura.jar")
    modImplementation files("libs/luaj-jse-3.0.8-figura.jar")
    modImplementation files("libs/modernkeybinding-1.20.X-1.2.0.jar")

}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
